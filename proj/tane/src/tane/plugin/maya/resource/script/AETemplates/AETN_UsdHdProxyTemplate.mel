global proc AETN_UsdHdProxy_FileBrowser(string $anAttr)
{
    string $fileNames[] = `fileDialog2 -caption "Specify USD File"
                                       -fileFilter "USD Files (*.usd*) (*.usd*);;Alembic Files (*.abc)"
                                       -fileMode 1`;
    if (size($fileNames) > 0)
    {
        evalEcho ("setAttr -type \"string\" " + $anAttr + " \"" + $fileNames[0] + "\"");
        refreshEditorTemplates;
    }


}

global proc AETN_UsdHdProxy_FilePathNew(string $anAttr)
{

        // setup ui template
        setUITemplate -pushTemplate attributeEditorTemplate;
        rowLayout -numberOfColumns 3;
            text -label "File Path";
            textField usdFilePathField;
            symbolButton -image "navButtonBrowse.xpm" usdFileBrowserButton;
        setParent ..;

        // wireup ae page
        AETN_UsdHdProxy_FilePathReplace($anAttr);
        // cleanup state
        setUITemplate -popTemplate;

}

global proc AETN_UsdHdProxy_FilePathReplace(string $anAttr)
{
        evalDeferred ("connectControl usdFilePathField " + $anAttr);
        button -edit -command ("AETN_UsdHdProxy_FileBrowser " + $anAttr) usdFileBrowserButton;
}

global proc AETN_UsdHdProxy_VariantSetsNew(string $attr)
{
    python("AETN_UsdHdProxyTemplate.variantSetsReplace('"+$attr+"', new=True)");

}
global proc AETN_UsdHdProxy_VariantSetsReplace(string $attr)
{
    python("AETN_UsdHdProxyTemplate.variantSetsReplace('"+$attr+"', new=False)");
}


global proc AETN_UsdHdProxyTemplate( string $nodeName )
{
    editorTemplate -beginScrollLayout;

    editorTemplate -beginLayout "USD" -collapse 0;
        editorTemplate -addControl "purpose";
        editorTemplate -callCustom "AETN_UsdHdProxy_FilePathNew" "AETN_UsdHdProxy_FilePathReplace" "filePath";
        editorTemplate -addControl "primPath";
        editorTemplate -addControl "excludePrimPaths";
        //editorTemplate -addControl "variantKey";
        //editorTemplate -addControl "complexity";
    editorTemplate -endLayout;

    editorTemplate -ccu AETN_UsdHdProxy_VariantSetsNew AETN_UsdHdProxy_VariantSetsReplace "";


    AEsurfaceShapeTemplate $nodeName;

    editorTemplate -addExtraControls;
    editorTemplate -endScrollLayout;
}
